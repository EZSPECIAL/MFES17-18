class User
instance variables
  private documentList: set of (Document) := {};
  private balance: real;

  inv balance >= 0;

operations
  -- Constructs a user with the specified parameters (balance)
  -- Balance must be greater than 0
  public User: real ==> User
    User(initBal) == (
      balance := initBal;
      return self;
    );

  -- Adds the specified document to the user's document list
  public addDocument: Document ==> ()
    addDocument(doc) == (
      documentList := documentList union {doc};
    )
    pre forall d in set documentList & doc.getDocName() <> d.getDocName();

  -- Removes specified document from user's document list
  public removeDocument: Document ==> ()
    removeDocument(doc) == documentList := documentList \ {doc};

  -- Credits the user account with the specified amount
  public deposit: real ==> ()
    deposit(amount) == balance := balance + amount;

  -- Debits the user account with the specified amount
  public withdraw: real ==> ()
    withdraw(amount) == balance := balance - amount;

-- GETTERS
  -- Returns the user's balance
  pure public getBalance: () ==> real
    getBalance() == return self.balance;

  -- Returns the user's document list
  pure public getDocumentList: () ==> set of (Document)
    getDocumentList() == return self.documentList;

end User